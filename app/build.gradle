apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        applicationId "org.fossasia.susi.ai"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        // for unit test
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug{
            minifyEnabled false
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a', 'mips', 'arm64-v8a', 'x86_64'
            universalApk false
        }
    }

    project.ext.versionCodes = [ 'armeabi-v7a': 1, 'arm64-v8a': 2, 'mips': 3, 'x86': 4, 'x86_64': 5]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1000000 + android.defaultConfig.versionCode
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$support_lib_version"
    compile "com.android.support:recyclerview-v7:$support_lib_version"
    compile "com.android.support:design:$support_lib_version"
    compile 'com.android.support:customtabs:24.2.1'


    compile 'me.himanshusoni.chatmessageview:chat-message-view:1.0.3'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.5'

    /*
    butterknife
    */
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    //For Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile "com.android.support:support-v4:$support_lib_version"

    //Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    debugCompile "com.squareup.okhttp3:logging-interceptor:3.3.1"

    //For Link Previews
    compile 'org.jsoup:jsoup:1.8.3' // required
    compile 'com.leocardz:link-preview:1.2.1@aar'

    //preference UI
    compile 'com.takisoft.fix:preference-v7:24.2.1.0'

    //unit test
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'

    //realm adapter
    compile 'io.realm:android-adapters:1.3.0'

    //waiting dots
    compile 'com.github.tajchert:WaitingDots:0.3.2'
}
